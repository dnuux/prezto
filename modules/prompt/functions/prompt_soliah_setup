#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   soliah <soliah@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

function prompt_soliah_precmd {
  vcs_info
}

function prompt_soliah_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_soliah_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:git:*' formats "%F{blue}%s:%F{red}%b%f"

  # Define prompts.
  PROMPT="%F{blue}%n%f at %F{red}%m%f in %F{blue}%~%f "'${vcs_info_msg_0_}'"
$ "
  SPROMPT="zsh: correct %F{red}%R%f to %F{blue}%r%f [nyae]? "
}

prompt_soliah_setup "$@"

